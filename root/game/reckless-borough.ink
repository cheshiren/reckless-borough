# hypertext
# single_scene
// # scenes_align: top
// # debug
# toolbar: game_toolbar

# title: Поход за покупками в Лихой Город
# author: Вячеслав Добранов

VAR title = "Поход за покупками в Лихой Город"

VAR ph1_link = 0
VAR ph2_link = 0
VAR ph3_link = 0
VAR ph4_link = 0
VAR ph5_link = 0
VAR ph6_link = 0
VAR ph7_link = 0
VAR ph8_link = 0
VAR ph9_link = 0

VAR ph1_label = 0
VAR ph2_label = 0
VAR ph3_label = 0
VAR ph4_label = 0
VAR ph5_label = 0
VAR ph6_label = 0
VAR ph7_label = 0
VAR ph8_label = 0
VAR ph9_label = 0
VAR ph10_label = "Папа"

VAR ph1_obj = 0
VAR ph2_obj = 0
VAR ph3_obj = 0
VAR ph4_obj = 0
VAR ph5_obj = 0
VAR ph6_obj = 0
VAR ph7_obj = 0
VAR ph8_obj = 0
VAR ph9_obj = 0

LIST objs = (market_obj), (mecha_obj), (_67_obj), (jaipur_obj), (red_obj), (dog_obj), (coffee_obj), (_158_obj), (pharm_obj)
~ ph1_obj = LIST_RANDOM(objs)
~ objs -= ph1_obj
~ ph2_obj = LIST_RANDOM(objs)
~ objs -= ph2_obj
~ ph3_obj = LIST_RANDOM(objs)
~ objs -= ph3_obj
~ ph4_obj = LIST_RANDOM(objs)
~ objs -= ph4_obj
~ ph5_obj = LIST_RANDOM(objs)
~ objs -= ph5_obj
~ ph6_obj = LIST_RANDOM(objs)
~ objs -= ph6_obj
~ ph7_obj = LIST_RANDOM(objs)
~ objs -= ph7_obj
~ ph8_obj = LIST_RANDOM(objs)
~ objs -= ph8_obj
~ ph9_obj = LIST_RANDOM(objs)
~ objs -= ph9_obj

~ init_ph(ph1_obj, ph1_link, ph1_label)
~ init_ph(ph2_obj, ph2_link, ph2_label)
~ init_ph(ph3_obj, ph3_link, ph3_label)
~ init_ph(ph4_obj, ph4_link, ph4_label)
~ init_ph(ph5_obj, ph5_link, ph5_label)
~ init_ph(ph6_obj, ph6_link, ph6_label)
~ init_ph(ph7_obj, ph7_link, ph7_label)
~ init_ph(ph8_obj, ph8_link, ph8_label)
~ init_ph(ph9_obj, ph9_link, ph9_label)

~ init_doubles()

////////////// DELETEME //////////////
// -> photos
// -> photo10
// -> interlude
////////////// DELETEME //////////////

# CLASS: second-face
…Ты раскрываешь детскую книгу и из неё высыпается ворох цветных прямоугольников — с десяток фотографий разлетаются по кровати. Изображения на пластинках статичные, но судя по слабому ЭМИ, какая-то логика в них всё же вшита. Ты машинально делаешь несколько жестов пальцами и джинн сканирует каждый из прямоугольников. Всё чисто. Вздыхаешь, подключаешь мета-данные снимков к своему цифровому слою и тянешься к одному из ярлыков, повисших над фотографиями…

+ [>>>(clickable=5 animation=5)] -> photos

= photos
~ title = "Поход за покупками в Лихой Город"

{TURNS_SINCE(ph1_link) == -1:
		<>[css class=unvisited][link=ph1]{ph1_label}[/link][/css]<>
	- else:
		<>[css class=visited][link=ph1]{ph1_label}[/link][/css]<>
}
{TURNS_SINCE(ph2_link) == -1:
		<>[css class=unvisited][link=ph2]{ph2_label}[/link][/css]<>
	- else:
		<>[css class=visited][link=ph2]{ph2_label}[/link][/css]<>
}
{TURNS_SINCE(ph3_link) == -1:
		<>[css class=unvisited][link=ph3]{ph3_label}[/link][/css]<>
	- else:
		<>[css class=visited][link=ph3]{ph3_label}[/link][/css]<>
}
{TURNS_SINCE(ph4_link) == -1:
		<>[css class=unvisited][link=ph4]{ph4_label}[/link][/css]<>
	- else:
		<>[css class=visited][link=ph4]{ph4_label}[/link][/css]<>
}
{TURNS_SINCE(ph5_link) == -1:
		<>[css class=unvisited][link=ph5]{ph5_label}[/link][/css]<>
	- else:
		<>[css class=visited][link=ph5]{ph5_label}[/link][/css]<>
}
{TURNS_SINCE(ph6_link) == -1:
		<>[css class=unvisited][link=ph6]{ph6_label}[/link][/css]<>
	- else:
		<>[css class=visited][link=ph6]{ph6_label}[/link][/css]<>
}
{TURNS_SINCE(ph7_link) == -1:
		<>[css class=unvisited][link=ph7]{ph7_label}[/link][/css]<>
	- else:
		<>[css class=visited][link=ph7]{ph7_label}[/link][/css]<>
}
{TURNS_SINCE(ph8_link) == -1:
		<>[css class=unvisited][link=ph8]{ph8_label}[/link][/css]<>
	- else:
		<>[css class=visited][link=ph8]{ph8_label}[/link][/css]<>
}
{TURNS_SINCE(ph9_link) == -1:
		<>[css class=unvisited][link=ph9]{ph9_label}[/link][/css]<>
	- else:
		<>[css class=visited][link=ph9]{ph9_label}[/link][/css]<>
}
<span>
{TURNS_SINCE(ph1_link) > 0 && TURNS_SINCE(ph2_link) > 0 && TURNS_SINCE(ph3_link) > 0 && TURNS_SINCE(ph4_link) > 0 && TURNS_SINCE(ph5_link) > 0 && TURNS_SINCE(ph6_link) > 0 && TURNS_SINCE(ph7_link) > 0 && TURNS_SINCE(ph8_link) > 0 && TURNS_SINCE(ph9_link) > 0:
	<br><br>[css class=goto][link=interlude]›[/link][/css]
	+ [interlude] -> interlude
}

+ [ph1] -> ph1_link(ph1_label)
+ [ph2] -> ph2_link(ph2_label)
+ [ph3] -> ph3_link(ph3_label)
+ [ph4] -> ph4_link(ph4_label)
+ [ph5] -> ph5_link(ph5_label)
+ [ph6] -> ph6_link(ph6_label)
+ [ph7] -> ph7_link(ph7_label)
+ [ph8] -> ph8_link(ph8_label)
+ [ph9] -> ph9_link(ph9_label)
- ->DONE

= market(label)
~ title = label
{not cooling_towers_market:
	~ cooling_towers++
	~ cooling_towers_market = cooling_towers
}
Практически идеально выверенная перспектива — главный проход, сжатый уступами прилавков, разрезает пространство рынка, начинаясь от места съёмки и устремляясь в туманный фокус. Зигзаги гирлянд и фонариков расчерчивают небо над проходом, пёстрая толпа покупателей перемешивается с многоцветием товаров.

{cooling_towers_market:
	- 1: Далеко на фоне поднимаются из тумана колоссы трёх градирен, уже более двадцати лет не работающих. Их серая вертикальная поверхность покрыта жилыми постройками [button=squat]сквоттеров[/button], как киль корабля ракушками.
	- 2: Далеко на фоне поднимаются из тумана всё те же градирни.
}

В воздухе висит желтоватая дымка [button=pollen]пыльцы[/button]. Её разводы покрывают внутреннюю чашу самодельного влагоуловителя, чей блеск в углу — единственное, что нарушает симметрию снимка.

[css class=goto][link=photos]‹[/link][/css]
+ [photos] -> photos

= mecha(label)
~ title = label
{not boy_mecha:
	~ boy++
	~ boy_mecha = boy
}
{not cooling_towers_mecha:
	~ cooling_towers++
	~ cooling_towers_mecha = cooling_towers
}
Селфи. <>
{boy_mecha:
	- 1: Веснушчатое лицо мальчика лет одиннадцати с копной рыжих волос.
	- 2: Снова рыжий мальчик.
}
<> Он щурится на ярком солнце и широко улыбается, возможно, смеётся. Рядом с ним жмурится и показывает язык другой мальчик, его виски выбриты, на скулах — характерные красные полосы от VR-гарнитуры. С другой стороны от оператора — задумчивая девушка в хиджабе держит на руках малолетнего брата. Малыш тянет в объектив свою игрушку — красно-белого пластикового робота-ме́ха без руки.

{cooling_towers_mecha:
	- 1: На фоне детей видны колоссы трёх градирен, уже более двадцати лет не работающих. Их серая вертикальная поверхность покрыта жилыми постройками [button=squat]сквоттеров[/button], как киль корабля ракушками.
	- 2: На их фоне — всё те же градирни.
}

[css class=goto][link=photos]‹[/link][/css]
+ [photos] -> photos

= _67(label)
~ title = label
Под оранжевым навесом из поликарбоната — террасы лотков с фруктами. Манго, ананасы, бананы, гуава, апельсины, ямс, рамбутан, мангустины, абрикосы, гранаты, киви, личи, инжир, маракуйя, папайя. Солнечный оранжевый свет, пропущенный сквозь навес, преломляется в водяной взвеси, распыляемой невидимыми форсунками. Над душистыми, росяными развалами висит минирадуга.

На ближние к выходу пирамидки орехов брошены несколько мятых листов так, чтобы их было видно от главного прохода. На листах, периодически меняя язык, мигают сообщения:  
«Все фрукты одного производителя! Идентичные естественно выращенным!»
«Содержание генно-модифицированных тканей — не более 67%!»

[css class=goto][link=photos]‹[/link][/css]
+ [photos] -> photos


= jaipur(label)
~ title = label
{not sideway_jaipur:
	~ sideway++
	~ sideway_jaipur = sideway
}
{sideway_jaipur:
	- 1: Вид сквозь боковой проход:
	- 2: Вид сквозь ещё один боковой проход:
}
<> редкая толпа окружила площадку, на которой группа подростков играет в [button=kabaddi]кабадди[/button]. Снимок сделан как раз в тот момент, когда нападающий одной из команд прыгает, чтобы вернуться на свою половину. Его загорелая рука вытянулась к разделительной линии, пытаясь достать её самыми кончиками пальцев. Двое оппонентов повисли у него на ногах, третий тянет за поношенную розовую джерси с логотипом [button=jpp]«Jaipur Pink Panthers»[/button].

Предельное напряжение на мокрых от пота лицах, белозубые оскалы, пыльные руки. Глаз не видно — все участники в линзах, через которые ИскИн показывает им состояние игры: счёт, границы площадки, статистику игроков. За отдельную плату — тренерские советы и отключение рекламы. За отдельную договорённость с устроителями — развесовку ставок.

[css class=goto][link=photos]‹[/link][/css]
+ [photos] -> photos

= red(label)
~ title = label
{not inner_space_red:
	~ inner_space++
	~ inner_space_red = inner_space
}
{not boy_red:
	~ boy++
	~ boy_red = boy
}
{not man_red:
	~ man++
	~ man_red = man
}
{not arm_red:
	~ arm++
	~ arm_red = arm
}
{inner_space_red:
	- 1: Снимок, снятый внутри уличной закусочной.
	- 2: Снова снимок изнутри помещения. На этот раз уличной закусочной.
}
<> На переднем плане — белый кафель прилавка, заставленного суповыми чашками, специями, упаковками палочек, соусными бутылками, салфетницами. За прилавком — тесная кухня, в которой быстро и ловко — как танцовщики — работают сутулый старик и его помощница-подросток: тянут лапшу из брикетов мицелия, шинкуют яркие овощи с тиснёнными [button=logos]лого[/button], нарезают круглые ломтики из цилиндров искусственной говядины. В другом углу кухни, в клубах пара, робот-манипулятор переливает горячий бульон из кастрюли в кастрюлю. На его верхний сустав повязан красный платок, такой же как у хозяев.

Задняя стенка закусочной — зеркальная, в ней отражается <>
{boy_red:
	- 1: веснушчатый рыжий мальчик лет одиннадцати
	- 2: рыжий мальчик
}
<>, снимающий из-за прилавка на большой винтажный [button=polaroid]Полароид[/button], и <>
{man_red:
	- 1: угрюмый
	- 2: тот же
}
<> седой мужчина рядом. <>
{arm_red:
	- 1: Вместо правой руки у мужчины — восьмипалый [button=prothesis]протез[/button] бывшего военного.
	- 2: Видно, что восьмипалый протез с прошлого фото — это правая рука мужчины.
}
<> Четыре палочки, зажатые в нём, удерживают сразу полчашки лапши. За их спинами — очередь посетителей и слепящий полуденный зной.

[css class=goto][link=photos]‹[/link][/css]
+ [photos] -> photos

= dog(label)
~ title = label
Инженерная [button=aic]«Айка»[/button] лежит под прилавком, прячась от зноя. Когда-то в прошлом — боевая единица, полудрон-полусобака. В последствии, после отключения от сети и тактического джинна — только полусобака.

Вместо правой передней лапы — титановая полусфера на тонкой, сложносочленённой конечности. К матово-гладкому титановому черепу прикреплены пара неуклюжих глаз — явный новодел, уступающий изяществу старых военных протезов. Эмпатическая попытка хозяина восстановить зрение отключённому инвалиду. Заодно, может быть, и слух. Но точно не нюх.  
На нагруднике, поверх едва угадываемой эмблемы ЧВК, кто-то нацарапал ехидное [button=three]«三足狗»[/button].

Весь этот титан и кремний наверняка ещё числятся где-то на орбитальный серверах, тысячи раз сменившие владельцев и тысячи раз забытые, архивированные с примечанием «Вернуть после окончания службы и/или смерти животного».

[css class=goto][link=photos]‹[/link][/css]
+ [photos] -> photos

= coffee(label)
~ title = label
{not arm_coffee:
	~ arm++
	~ arm_coffee = arm
}
{not sack_coffee:
	~ sack++
	~ sack_coffee = sack
}
{arm_coffee:
	- 1: Крупный план старого военного [button=prothesis]протеза[/button] с восемью пальцами, которые сжимают <>
	{sack_coffee:
		- 1: полукилограммовый джутовый мешок
		- 2: джутовый мешок с прошлого снимка
	}
	<>.
	- 2: Крупный план: в восьмипалом протезе зажат <>
	{sack_coffee:
		- 1: полукилограммовый джутовый мешок
		- 2: джутовый мешок с прошлого снимка
	}
	<>.
}
<> Сверху мешок прострочен красной нитью, её концы спаяны круглой печатью с оттиском радиальной [button=datamatrix]датаматрицы[/button]. Красными же чернилами на мешок нанесена надпись: [button=hydro]«Café hidropônico de São Paulo»[/button].

Сбоку к мешку подвязан пучок коричных палочек, слишком одинаковых, чтобы быть натуральными.

{arm_coffee:
	- 1: На одном из
	- 2: При таком увеличении на одном из восьми
}
<> титановых пальцев различима кольцевая гравировка: «Спаси и сохрани».

[css class=goto][link=photos]‹[/link][/css]
+ [photos] -> photos

= _158(label)
~ title = label
{not sideway_158:
	~ sideway++
	~ sideway_158 = sideway
}
Снимок <>
{sideway_158:
	- 2: другого
}
<> бокового прохода. В центре — молодой рабочий показывает в камеру жест с оттопыренными большим пальцем и мизинцем. На его плечах — по стокилограммовой бухте кабеля. На лицо надета «маска должника»: несъёмный респиратор с контейнерами нейролептика. Расширенные зрачки спрятаны от полуденного солнца за чёрными линзами. На лбу — широкий кусок пластыря с цифрами [button=theft]«158»[/button]. Ремни плохо подогнанного экзоскелета натёрли кожу. Голый тощий торс мокрый от пота, цветастые шорты, сланцы.

За его спиной двое других рабочих в точно таких же масках и экзоскелетах откапывают старую кабельную трассу. Над ними висит микродрон с чёрным зрачком камеры — кто-то удалённо следит за работой.

Ни муниципальная власть, ни корпоративные суверенитеты не распространяются на территорию Лихого Города, поэтому рабочие, скорее всего, отрабатывают свой долг перед местным синдикатом.

[css class=goto][link=photos]‹[/link][/css]
+ [photos] -> photos

= pharm(label)
~ title = label
{not inner_space_pharm:
	~ inner_space++
	~ inner_space_pharm = inner_space
}
{not man_pharm:
	~ man++
	~ man_pharm = man
}
{not sack_pharm:
	~ sack++
	~ sack_pharm = sack
}
{inner_space_pharm:
	- 1: Снимок, снятый внутри тесной аптеки.
	- 2: Снова снимок изнутри помещения. На этот раз уличной аптеки.
}
<> Прозрачная перегородка отделяет зону покупателей от остального помещения. На переднем плане, за перегородкой — стеклянный куб фарм-принтера. Стробоскопические вспышки в его рабочем объёме — «печатается» заказанное кем-то лекарство: нужные молекулы спекаются из газообразного сырья по инструкциям из лицензионного рецепта.

На ближайшей стенке принтера — большая наклейка с текстом, каждые пару минут сменяющим язык:<br><>
• Предупреждение об уголовной ответственности за использование нелицензионных матриц;<br><>
• Предупреждение о федеральном законе по защите авторских прав, допускающему вред здоровью нарушителя;<br><>
• Реклама тестовых препаратов по низким ценам.

На заднем плане — шкафы, заставленные склянками с надписями [button=devanagari]деванагари[/button]. Под ними старик что-то мешает в каменной ступке, не обращая внимания на покупателей.

В правой стороне снимка — <>
{man_pharm:
	- 1: угрюмый седой мужчина
	- 2: уже знакомый седой мужчина
}
<> изучает рецепт на листе пластика, криво приклеенного с внутренней стороны перегородки. Подмышкой мужчина держит <>
{sack_pharm:
	- 1: какой-то джутовый мешок
	- 2: мешок с кофе
}
<> и красный [button=cig]сигаретный блок[/button].

[css class=goto][link=photos]‹[/link][/css]
+ [photos] -> photos

= interlude
# CLASS: second-face
…Ты замечаешь ещё одну фотографию под всеми остальными. Если бы не моргнувший ярлык, ты бы её даже не увидел…
+ [>>>(clickable=3 animation=3)] -> photo10

= photo10
[css class=unvisited last-photo][link=ph10]{ph10_label}[/link][/css]
+ [ph10] -> dad

= dad
Групповое фото у входа на Рынок. Все дети собрались для общего снимка. В центре — рыжий мальчик в инвалидной коляске. Улыбается, машет в камеру, держит на коленях подаренного робота-ме́ха. Седой ветеран стоит рядом, положив левую — здоровую — руку мальчику на плечо. Он не улыбается, просто смотрит в камеру. В другой руке у него — белый пакет с покупками.

За их спинами сквот, облепивший градирни, выпускает первых воздушных змеев, чтобы поймать вечерние восходящие потоки.

Пора возвращаться домой.

[css class=goto][link=outro]‹[/link][/css]
+ [outro] -> outro

= outro
# CLASS: second-face
…Ты задерживаешь внимание на неулыбчивом лице мужчины. Коротко остриженные, рано поседевшие волосы. Плотно сжатый рот. За взглядом серых глаз — затаённая бессильная ярость. Взгляд отца, который не в силах поставить своего ребёнка на ноги в несправедливом мире, полном технологических чудес.
# CLASS: second-face
«Да уж. Подвели мы тебя, а, Серёг?» — ты переводишь взгляд на единственное окно, за которым всё падает и падает снег. В погожий день отсюда — с самой вершины Сквота — наверное, отличный вид на весь Лихой Город.
# CLASS: second-face
Потом встряхиваешься, собираешь все фотографии обратно в книгу, даёшь команду джинну отослать все новые данные в офис и продолжаешь обыск. До обеда ещё час, нужно успеть осмотреть остальное помещение…
+ [>>>(clickable=10 animation=10)] -> theend

= theend
# CLASS: centered
Вячеслав Добранов<br>2025
-> END

=== function game_toolbar()
	{title}

=== function squat()
	[title]Сквот[/title]
	[table]<>
		[header]ИскИн - справка[/header]<>
		[row]Незаконные постройки на территории бывшей ТЭЦ-21 по адресу: Москва, ул. Ижорская, 9.[/row]<>
		[row]ТЭЦ выведена из эксплуатации в 2031 году, с тех пор территория официально заброшена.[/row]<>
		[row]С 2036 происходит стихийный самозахват маргинальными элементами, ускоренный послевоенным кризисом.[/row]<>
		[row]Основная часть населения — беженцы с Индийского субконтинента и Юго-Восточной Азии.[/row]<>
		[row]В прошлом году коллегией ЮИИ по САО г. Москвы принято решение по продаже территории бывшей ТЭЦ в собственность ██████████████ с установлением над ней корпоративного суверенитета.[/row]<>
		[row]Ликвидация незаконного жилья и снос строений запланированы на будущий год.[/row]<>		
	[/table]

=== function pollen()
	[title]Пыльца[/title]
	[table]<>
		[header]ИскИн - справка[/header]<>
		[row]Пыльца одной из мутаций берёзы пушистой. Появление новых, обильно пылящих, видов — побочный фактор обширной генной модификации сельскохозяйственных культур на территории Китайской Сибири.[/row]<>
		[row]В 2042 году создан межкорпоративный орган для поиска решений по сохранению «старых» видов флоры и купированию распространения новых.[/row]<>
	[/table]
=== function kabaddi()
	[title]Кабадди[/title]
	[table]<>
		[header]ИскИн - справка[/header]<>
		[row]Командная игра с элементами борьбы и салок. Одна из древнейших известных игр, изначально распространённая среди народов Индии.[/row]<>
		[row]Повсеместное распространение получила с потоками беженцев, расселившихся по миру.[/row]<>
		[row]Популярность обусловлена так же отсутствием необходимого спортинвентаря — достаточно ровной прямоугольной площадки.[/row]<>
	[/table]
=== function jpp()
	[title]Jaipur Pink Panthers[/title]
	[table]<>
		[header]ИскИн - справка[/header]<>
		[row]Изначально, JPP — команда Профессиональной Лиги Кабадди из Джайпура, штат Раджастхан.[/row]<>
		[row]До закрытия «старой» Лиги — обладательница пяти чемпионских титулов.[/row]<>
		[row]После воссоздания ПЛК в 2036 году трижды выходила в полуфинал, но титулы не выигрывала.[/row]<>
		[row]Характерна агрессивной игрой с упором на силовые и акробатические приёмы. Соотношение физических и конгитивных приращений у игроков: 64/36.[/row]<>
		[row]Штаб-квартира расположена в городе Перт, Западная Австралия. Главным спонсором выступает конгломерат «Маккуори-COSCO Юг».[/row]<>
	[/table]
=== function logos()
	[title]Лого[/title]
	[table]<>
		[header]ИскИн - справка[/header]<>
		[row]«Xianbeilia Agricultural Equipment»[/row]<>
		[row]«Bayer CropScience»[/row]<>
		[row]«COFCO Россия»[/row]<>
	[/table]
=== function polaroid()
	[title]Полароид[/title]
	[table]<>
		[header]ИскИн - справка[/header]<>
		[row][/row]<>
	[/table]
=== function prothesis()
	[title]Протез[/title]
	[table]<>
		[header]ИскИн - справка[/header]<>
		[row][/row]<>
	[/table]
=== function aic()
	[title]AIC[/title]
	[table]<>
		[header]ИскИн - справка[/header]<>
		[row][/row]<>
	[/table]
=== function three()
	[title]三足狗[/title]
	[table]<>
		[header]ИскИн - справка[/header]<>
		[row]«Трёхлапая собака» (кит. упр. 三足狗, пиньинь <i>sān zú gǒu</i>).[/row]<>
	[/table]
=== function datamatrix()
	[title]Датаматрица[/title]
	[table]<>
		[header]ИскИн - справка[/header]<>
		[row][/row]<>
	[/table]
=== function hydro()
	[title]Café hidropônico de São Paulo[/title]
	[table]<>
		[header]ИскИн - справка[/header]<>
		[row]«Гидропонный кофе из Сан-Паулу» (порт. <i>Café hidropônico de São Paulo</i>).[/row]<>
		[row]«Сан-Паулу», в данном случае, не место производства, а торговая марка. Гидропонные технологии позволяют выращивать нужные культуры в любых географиях.[/row]<>
		[row]Среднегодовые температуры окружающей среды в мире не позволяют растить кофе большими урожаями. Натурально выращенный кофе относится к продуктам премиум-сегмента.[/row]<>
	[/table]
=== function theft()
	[title]158[/title]
	[table]<>
		[header]ИскИн - справка[/header]<>
		[row][/row]<>
	[/table]
=== function devanagari()
	[title]Деванагари[/title]
	[table]<>
		[header]ИскИн - справка[/header]<>
		[row][/row]<>
	[/table]
=== function cig()
	[title]Сигареты[/title]
	[table]<>
		[header]ИскИн - справка[/header]<>
		[row][/row]<>
	[/table]

=== function init_ph(obj, ref link, ref label)
	{obj:
	- market_obj:
		~ link = -> market
		~ label = "Рынок"
	- mecha_obj:
		~ link = -> mecha
		~ label = "Ме́ха"
	- _67_obj:
		~ link = -> _67
		~ label = "Не более 67%"
	- jaipur_obj:
		~ link = -> jaipur
		~ label = "Розовые пантеры Джайпура"
	- red_obj:
		~ link = -> red
		~ label = "Красные платки"
	- dog_obj:
		~ link = -> dog
		~ label = "三足狗"
	- coffee_obj:
		~ link = -> coffee
		~ label = "Café hidropônico"
	- _158_obj:
		~ link = -> _158
		~ label = "Один-пятьдесят восемь"
	- pharm_obj:
		~ link = -> pharm
		~ label = "Фарм-принтер"
	}

=== function init_doubles()
	VAR cooling_towers = 0
	VAR boy = 0
	VAR man = 0
	VAR arm = 0
	VAR sack = 0
	VAR sideway = 0
	VAR inner_space = 0

	VAR cooling_towers_market = 0
	VAR cooling_towers_mecha = 0
	VAR boy_mecha = 0
	VAR boy_red = 0
	VAR man_red = 0
	VAR man_pharm = 0
	VAR arm_red = 0
	VAR arm_coffee = 0
	VAR sack_coffee = 0
	VAR sack_pharm = 0
	VAR sideway_jaipur = 0
	VAR sideway_158 = 0
	VAR inner_space_red = 0
	VAR inner_space_pharm = 0